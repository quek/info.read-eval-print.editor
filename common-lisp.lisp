(in-package :info.read-eval-print.editor)

(define-mode common-lisp-mode () ())

(add-auto-mode-alist 'common-lisp-mode "\\.lisp$")


(define-command indent :in common-lisp-mode ()
  (funcall (read-from-string "INFO.READ-EVAL-PRINT.EDITOR.COMMON-LISP.INDENT:INDENT-LINE")
           (info.read-eval-print.editor.command::point)))


(defun forward-sexp (iter &optional (count 1))
  (forward-skip-whitespace iter)
  (dotimes (i count)
    (let ((char (text-iter-char iter)))
      (cond ((char= #\( char)
             (loop with level = 1
                   for i = (text-iter-move iter)
                   for c = (text-iter-char iter)
                   until (or (zerop level) (text-iter-is-end iter))
                   if (char= #\( c)
                     do (incf level)
                   else if (char= #\) c)
                          do (decf level)))
            ((char= #\) char)
             (text-iter-move iter))
            (t (loop until (or (text-iter-is-end iter)
                               (whitespace-p (text-iter-char iter)))
                     do (text-iter-move iter)))))
    (forward-skip-whitespace iter)))

(defun end-of-sexp (iter &optional (count 1))
  (dotimes (i count)
    (text-iter-move iter)
    (forward-skip-whitespace iter)
    (let ((char (text-iter-char iter)))
      (cond ((char= #\( char)
             (loop with level = 1
                   for i = (text-iter-move iter)
                   for c = (text-iter-char iter)
                   until (or (zerop level) (text-iter-is-end iter))
                   if (char= #\( c)
                     do (incf level)
                   else if (char= #\) c)
                          do (decf level)))
            ((char= #\) char))
            (t (loop until (text-iter-is-end iter)
                     until (and (whitespace-p (text-iter-char iter))
                                (progn
                                  (text-iter-move iter :direction :backward)
                                  t))
                     do (text-iter-move iter)))))))

(defun backward-sexp (iter &optional (count 1))
  (dotimes (i count)
    (text-iter-move iter :direction :backward)
    (backward-skip-whitespace iter)
    (let ((char (text-iter-char iter)))
      (cond ((char= #\) char)
             (loop with level = 1
                   for i = (text-iter-move iter :direction :backward)
                   for c = (text-iter-char iter)
                   if (char= #\) c)
                     do (incf level)
                   else if (char= #\( c)
                          do (decf level)
                   until (or (zerop level) (text-iter-is-start iter))))
            ((char= #\( char))
            (t (loop for c = (text-iter-char iter)
                     until (text-iter-is-start iter)
                     until (and (or (whitespace-p c)
                                    (char= #\( c)
                                    (char= #\) c))
                                (progn (text-iter-move iter)
                                       t))
                     do (text-iter-move iter :direction :backward)))))))

(defun last-sexp (buffer)
  (let ((start (iter-at-mark buffer))
        (end (iter-at-mark buffer)))
    (backward-sexp start)
    (forward-sexp end)
    (insert buffer (prin1-to-string (eval (read-from-string (slice buffer start end)))))))

(defun up-list (iter)
  (unless (text-iter-is-start iter)
    (text-iter-move iter :direction :backward)
    (unless (char= #\( (text-iter-char iter))
      (backward-sexp iter)
      (up-list iter))))

(defun down-list (iter)
  (unless (text-iter-is-end iter)
    (text-iter-move iter)
    (unless (char= #\( (text-iter-char iter))
      (down-list iter))))


(define-command eval-last-sexp ()
  (last-sexp *buffer*))

(define-command forward-sexp (&optional (count *digit-argument*))
  (let ((iter (iter-at-mark *buffer*)))
    (forward-sexp iter count )
    (update-cursor *buffer* iter)))

(define-command backward-sexp (&optional (count *digit-argument*))
  (let ((iter (iter-at-mark *buffer*)))
    (backward-sexp iter count)
    (update-cursor *buffer* iter)))

(define-command end-of-sexp (&optional (count *digit-argument*))
  (let ((iter (iter-at-mark *buffer*)))
    (end-of-sexp iter count)
    (update-cursor *buffer* iter)))

(define-command up-list (&optional (count *digit-argument*))
  (let ((iter (iter-at-mark *buffer*)))
    (loop repeat count do (up-list iter))
    (update-cursor *buffer* iter)))

(define-command-alias up-list backward-up-list)

(define-command down-list (&optional (count *digit-argument*))
  (let ((iter (iter-at-mark *buffer*)))
    (dotimes (i count)
      (down-list iter))
    (update-cursor *buffer* iter)))

(define-command beginning-of-defun ()
  (let ((iter (iter-at-mark *buffer*)))
    (multiple-value-bind (p s e) (text-iter-search iter "(defun" :direction :backward)
      (declare (ignore e))
      (when p
        (update-cursor *buffer* s)))))

(defun search-buffer-package ()
  (let ((re "^\\((cl:\\|common-lisp:)?in-package\\b[ \\t']*([^\\)]+)[ \\t]*\\)"))
    (when (or (info.read-eval-print.editor.command::re-search-backward re)
              (info.read-eval-print.editor.command::re-search-forward re))
      (info.read-eval-print.editor.command::match-string-no-properties  2))))

(define-command current-package ()
  (or (ignore-errors (find-package (string-upcase (search-buffer-package))))
      (find-package :cl-user)))

(define-command defun-at-point ()
  (save-excursion
    (info.read-eval-print.editor.command::beginning-of-defun)
    (let ((start (info.read-eval-print.editor.command::point)))
      (info.read-eval-print.editor.command::forward-sexp)
      (let ((end (info.read-eval-print.editor.command::point)))
        (quek:p start end)
        (info.read-eval-print.editor.command:buffer-substring-no-properties start end)))))

(define-command eval-defun ()
  (let ((defun-form (info.read-eval-print.editor.command::defun-at-point))
        (*package* (info.read-eval-print.editor.command::current-package)))
    (handler-case (message (eval (read-from-string defun-form)))
      (error (e)
        (open-info-frame (format nil "(in-package :~a)~%~%~a~%~%~a"
                                 (string-downcase (package-name *package*))
                                 defun-form
                                 e))))))

(define-command compile-and-load-file ()
  (open-info-frame
   (with-output-to-string (*standard-output*)
     (let ((*error-output* *standard-output*))
       (load (compile-file (file-of *buffer*)))))))

(defun symbol-at-point ()
  (save-excursion
    (let ((start (iter-at-mark *buffer*))
          (end (iter-at-mark *buffer*)))
      (backward-sexp start)
      (text-buffer-slice *buffer* start end))))

(define-command complete-symbol ()
  (let* ((symbol (symbol-at-point))
         (symbols (car (swank:simple-completions symbol (search-buffer-package)))))
    (if (len=1 symbols)
        (progn
          (let ((end (iter-at-mark *buffer*))
                (start (iter-at-mark *buffer*)))
            (backward-sexp start)
            (text-buffer-delete *buffer* start end)
            (insert *buffer* (car symbols))))
        (open-info-frame (with-output-to-string (out)
                                 (iterate ((x (scan symbols)))
                                   (format out "~a~%" x)))))))

(define-command indent-and-complete-symbol ()
  (info.read-eval-print.editor.command::indent)
  (info.read-eval-print.editor.command::complete-symbol))

(loop for (mode keyseq command)
      in `((:normal (:meta #\h) info.read-eval-print.editor.command::backward-sexp)
           (:insert (:meta #\h) info.read-eval-print.editor.command::backward-sexp)
           (:normal (:meta #\j) info.read-eval-print.editor.command::down-list)
           (:insert (:meta #\j) info.read-eval-print.editor.command::down-list)
           (:normal (:meta #\k) info.read-eval-print.editor.command::up-list)
           (:insert (:meta #\k) info.read-eval-print.editor.command::up-list)
           (:normal (:meta #\l) info.read-eval-print.editor.command::forward-sexp)
           (:insert (:meta #\l) info.read-eval-print.editor.command::forward-sexp)
           (:normal (:meta #\e) info.read-eval-print.editor.command::eval-last-sexp)
           (:insert (:meta #\e) info.read-eval-print.editor.command::eval-last-sexp)
           (:normal (:meta #\x) info.read-eval-print.editor.command::eval-defun)
           (:insert (:meta #\x) info.read-eval-print.editor.command::eval-defun)
           (:normal (:meta #\k) info.read-eval-print.editor.command::compile-and-load-file)
           (:insert (:control #\i) info.read-eval-print.editor.command::indent-and-complete-symbol)
           (:insert (#\Tab) info.read-eval-print.editor.command::indent)
           (:insert (:control #\m) info.read-eval-print.editor.command::newline-and-indent)
           (:insert (#\Return) info.read-eval-print.editor.command::newline-and-indent))
      do (set-key *common-lisp-mode-map*  mode keyseq command))
